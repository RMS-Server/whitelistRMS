plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'net.rms.xrain'
version = '1.1.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'
    compileOnly 'org.slf4j:slf4j-api:1.7.30'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.1.4'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'com.google.code.gson:gson:2.10.1'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(AbstractArchiveTask) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

shadowJar {
    archiveBaseName.set(project.name)
    archiveClassifier.set('')
    archiveVersion.set(project.version.toString())
    
    // 确保包含所有依赖
    dependencies {
        include(dependency('org.mariadb.jdbc:mariadb-java-client'))
        include(dependency('com.zaxxer:HikariCP'))
        include(dependency('com.google.code.gson:gson'))
    }
    
    // 合并服务文件
    mergeServiceFiles()
}

tasks.build.dependsOn tasks.shadowJar

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}
